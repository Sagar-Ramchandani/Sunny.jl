"""
    print_wrapped_intensities(sys::System; nmax=10)

Prints up to `nmax` wavevectors ``ðª`` and their "wrapped" static structure
factor weights. Each ``ðª`` is exactly commensurate with the system volume and
has components between ``-1/2`` and ``1/2`` in reciprocal lattice units (RLU).
The output from this function will typically be used as input to
[`suggest_magnetic_supercell`](@ref).

For simplicity, phase interference between sublattices is neglected. The
reported weights are the sum of static structure factors
``\\mathcal{S}_{jj}(ðª)`` calculated independently for each sublattice ``j`` of
the chemical cell. This is mathematically equivalent to averaging
``\\mathcal{S}(ðª)`` over all cells of the infinite reciprocal lattice. It is in
this sense that the intensities are "wrapped" into the first reciprocal cell.

To calculate the true ``\\mathcal{S}(ðª)`` as an experimental observable, use
[`SampledCorrelationsStatic`](@ref) instead.
"""
function print_wrapped_intensities(sys::System{N}; nmax=10) where N
    sys.crystal == orig_crystal(sys) || error("Cannot perform this analysis on reshaped system.")

    s = reinterpret(reshape, Float64, sys.dipoles)
    V = prod(sys.dims) # number of spins in sublattice
    cell_dims = (2,3,4)
    sk = FFTW.fft(s, cell_dims) / âˆšV
    Sk = real.(conj.(sk) .* sk)
    dims = (1,5) # sum over spin index and atom (sublattice) index
    # In Julia 1.9 this becomes: sum(eachslice(dat; dims)))
    Sk = dropdims(sum(Sk; dims); dims)
    @assert sum(Sk) â‰ˆ norm(sys.dipoles)^2

    weight = Sk .* (100 / norm(sys.dipoles)^2)
    p = sortperm(-weight[:])

    println("Dominant wavevectors for spin sublattices:\n")
    for (i, m) in enumerate(CartesianIndices(sys.dims)[p])
        k = (Tuple(m) .- 1) ./ sys.dims
        k = [ki > 1/2 ? ki-1 : ki for ki in k]

        kstr = vec3_to_string(k)

        if weight[m] < 0.01
            break
        end

        wstr = @sprintf "%6.2f" weight[m]
        nspaces = 20
        spacing = " " ^ max(0, nspaces - length(kstr))
        print("    $kstr $spacing $wstr%")
        println(i == 1 ? " weight" : "")

        if i > nmax
            spacing = " " ^ (nspaces-1)
            println("    ... $spacing ...")
            break
        end
    end
end

function rationalize_simultaneously(xs; tol, maxsize)
    for denom = 1:maxsize
        numers = @. round(Int, xs * denom)
        errs = @. xs - numers / denom
        if all(e -> abs(e) < tol, errs)
            return numers .// denom
        end
    end
    error("Wavevectors are incommensurate for lattice sizes less than $maxsize. Try increasing `tol` parameter.")
end


"""
    suggest_magnetic_supercell(ks; tol=1e-12, maxsize=100)

Suggests a magnetic supercell, in units of the crystal lattice vectors, that is
consistent with periodicity of the wavevectors `ks` in RLU. If the wavevectors
are incommensurate (with respect to the maximum supercell size `maxsize`), one
can select a larger error tolerance `tol` to find a supercell that is almost
commensurate.

Prints a ``3Ã—3`` matrix of integers that is suitable for use in
[`reshape_supercell`](@ref).

# Examples

```julia
# A magnetic supercell for a single-Q structure. Will print
k1 = [0, -1/4, 1/4]
suggest_magnetic_supercell([k1])       # [1 0 0; 0 2 1; 0 -2 1]

# A larger magnetic supercell for a double-Q structure
k2 = [1/4, 0, 1/4]
suggest_magnetic_supercell([k1, k2])   # [1 2 2; -1 2 -2; -1 2 2]

# If given incommensurate wavevectors, find an approximate supercell that
# is exactly commensurate for nearby wavevectors.
suggest_magnetic_supercell([[0, 0, 1/âˆš5], [0, 0, 1/âˆš7]]; tol=1e-2)

# This prints [1 0 0; 0 1 0; 0 0 16], which becomes commensurate under the
# approximations `1/âˆš5 â‰ˆ 7/16` and `1/âˆš7 â‰ˆ 3/8`.
```
"""
function suggest_magnetic_supercell(ks; tol=1e-12, maxsize=100)
    eltype(ks) <: AbstractVector{<: Number} || error("Pass a list of wavevectors")

    rational_ks = zeros(Rational{Int}, 3, length(ks))
    for i in 1:3
        xs = [k[i] for k in ks]
        rational_ks[i, :] = rationalize_simultaneously(xs; tol, maxsize)
    end

    suggest_magnetic_supercell_aux(eachcol(rational_ks))
end

function suggest_magnetic_supercell_aux(ks)
    # A supercell of shape A = diagm(nmax) is guaranteed to be commensurate
    nmax = [lcm(denominator.(row)...) for row in zip(ks...)]
    ns = Vec3.(eachcol(diagm(nmax)))

    # Candidate lattice vectors for a smaller supercell
    rg = div.(nmax, 2)
    append!(ns, Vec3.(Iterators.product(-rg[1]:rg[1], -rg[2]:rg[2], -rg[3]:rg[3])))

    # Sort by length
    sort!(ns, by=n->n'*n)

    # Remove zero vector
    @assert iszero(ns[1])
    deleteat!(ns, 1)

    # Filter out elements of ns that are not consistent with k âˆˆ ks
    for k in ks
        ns = filter(ns) do n
            # Wave vector `k` in RLU is commensurate if `nâ‹…k` is integer,
            # corresponding to the condition `exp(-i 2Ï€ nâ‹…k) = 1`.
            isinteger(nâ‹…k)
        end
    end

    # Goodness of supervectors A1, A2, A3. Lower is better.
    function score(A)
        (A1, A2, A3) = eachcol(A)
        # Minimize supercell volume
        V = (A1 Ã— A2) â‹… A3
        # To split ties, maximize orthogonality
        c1 = (normalize(A1) Ã— normalize(A2)) â‹… normalize(A3)
        # To split ties, maximize alignment with Cartesian system
        c2 = normalize(A1)[1] + normalize(A2)[2] + normalize(A3)[3]
        V <= 0 ? Inf : V - 1e-3*c1 - 1e-6c2
    end

    # Initial guess for the supercell
    best_A = diagm(nmax)
    best_score = score(best_A)

    # Iteratively search for an improved supercell. For efficiency, restrict the
    # search to some maximum number of displacement vectors
    ns = first(ns, 1000)

    any_changed = true
    while any_changed
        any_changed = false

        # try all possible replacements of individual columns
        for j in 1:3, n in ns
            A = copy(best_A)
            A[:, j] = n
            if best_score > score(A)
                best_score = score(A)
                best_A = A
                any_changed = true
            end
        end

        # try all possible rotations
        (A1, A2, A3) = eachcol(best_A)
        for A in ([A1 -A3 A2], [A1 -A2 -A3], [A1 A3 -A2], # x-axis
                  [A3 A2 -A1], [-A1 A2 -A3], [-A3 A2 A1], # y-axis
                  [-A2 A1 A3], [-A1 -A2 A3], [A2 -A1 A3]) # z-axis
            if best_score > score(A)
                best_score = score(A)
                best_A = A
                any_changed = true
            end
        end
    end

    # # Alternative brute force implementation, for reference
    # all_As = ([A1 A2 A3] for (A1, A2, A3) in Iterators.product(ns, ns, ns))
    # @assert minimum(score, all_As) == best_score

    kstrs = join(map(vec3_to_string, ks), ", ")
    println("""Possible magnetic supercell in multiples of lattice vectors:
               
                   $(repr(best_A))
               
               for the rationalized wavevectors:
               
                   [$kstrs]""")
end


function check_commensurate(sys; k)
    q_reshaped = sys.crystal.recipvecs \ orig_crystal(sys).recipvecs * k
    commensurate = true
    for i = 1:3
        denom = denominator(rationalize(q_reshaped[i]))
        commensurate = commensurate && iszero(mod(sys.dims[i], denom))
    end
    if !commensurate
        @warn "Wavevector $(vec3_to_string(k)) is incommensurate with system."
    end
end


function deprecate_small_q(; q, k)
    if !isnothing(q)
        @warn "`q` argument is deprecated, use `k` to denote propagation wavevector instead!"
        k = q
    end
    isnothing(k) && error("Must provide wavevector `k` as named argument")
    return k
end

